// Top-level build file where you can add configuration options common to all sub-projects/modules.
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5'
        classpath 'com.android.tools.build:gradle:0.13.3'
    }
}

apply plugin: 'com.github.ben-manes.versions'

ext {
    compileSdkVersion = 21
    buildToolsVersion = "21.0.2"

    minSdkVersion = 16
    targetSdkVersion = '21'
    versionCode = 40
    versionName = "1.4.5"
}

allprojects {
    repositories {
        jcenter()
    }

    ext.androidTest = [
            'org.hamcrest:hamcrest-library:1.3',
            'org.objenesis:objenesis:2.1',
            'com.google.dexmaker:dexmaker-mockito:1.1',
            'org.mockito:mockito-core:1.9.5',
            'com.jayway.android.robotium:robotium-solo:5.2.1'
    ]
}

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}